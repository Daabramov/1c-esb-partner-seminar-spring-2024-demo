#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтправитьДанныеПоЗаявке(Заявка) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Сообщение = СервисыИнтеграции.КурьерскиеЗаявки.СоздатьСообщение();
	ВспомогательныйШина.ПоместитьОбъектВТелоСообщения(Сообщение, ДанныеЗаявкиВФормате(Заявка));
	СервисыИнтеграции.КурьерскиеЗаявки.Основной_КурьерскиеЗаявки_Channel1CSource.ОтправитьСообщение(Сообщение);

КонецПроцедуры


Функция ДанныеЗаявкиВФормате(ЗаявкаСсылка) Экспорт

	ЗапросДанных = Новый Запрос;
	ЗапросДанных.Текст =
	"ВЫБРАТЬ
	|	КурьерскаяЗаявка.Ссылка КАК Ссылка,
	|	КурьерскаяЗаявка.Автор КАК Автор,
	|	КурьерскаяЗаявка.Адрес КАК Адрес,
	|	Isnull(КурьерскаяЗаявка.Курьер.Наименование, Неопределено) КАК Курьер,
	|	КурьерскаяЗаявка.Комментарий КАК Комментарий,
	|	ЕСТЬNULL(СтатусыКурьерскихЗаявокСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыКурьерскихЗаявок.Создана)) КАК
	|		Статус,
	|	КурьерскаяЗаявка.Автор.Наименование КАК АвторНаименование
	|ИЗ
	|	Документ.КурьерскаяЗаявка КАК КурьерскаяЗаявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыКурьерскихЗаявок.СрезПоследних КАК СтатусыКурьерскихЗаявокСрезПоследних
	|		ПО СтатусыКурьерскихЗаявокСрезПоследних.Заявка = КурьерскаяЗаявка.Ссылка
	|ГДЕ
	|	КурьерскаяЗаявка.Ссылка = &Ссылка";
	ЗапросДанных.УстановитьПараметр("Ссылка", ЗаявкаСсылка);
	РезультатЗапроса = ЗапросДанных.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	ДанныеОтвета = Новый Структура(
		"id,authorId,authorName,address,courierName,comment,status",
		XMLСтрока(Выборка.Ссылка),
		XMLСтрока(Выборка.Автор),
		Выборка.АвторНаименование,
		Выборка.Адрес,
		Выборка.Курьер,
		Выборка.Комментарий,
		XMLСтрока(Выборка.Статус)
	);
	Возврат ДанныеОтвета;
КонецФункции


#КонецОбласти